// structure
?start: main
entry: call+			
call: loop* RULENAME
loop: count? "{" transform+ "}"
count: NUMBER "*"                   
transform: (TID | CID) VALUE+   

ruledef: "rule" RULENAME  mods*  "{" call+ "}"
?mods: md_mod | w_mod
main: global_md? entry ruledef+ 

// definitions
global_md: "set" ("md" | "maxdepth") NUMBER
md_mod: ("md" | "maxdepth") NUMBER [">" RULENAME]
w_mod: ("w" |"weight") NUMBER

RULENAME:  /[^\W\d]\w*/ 
TID: "x"|"y"|"z"|"s"|"rx"|"ry"|"rz"
CID: "h"|"hue"|"sat"|"b"|"brightness"|"a"|"alpha"|"m" |"c" |"color" | "blend"
VALUE: SIGNED_NUMBER | COLORHEX | COLORNAME
COLORHEX: "#" HEXDIGIT+ 
COLORNAME: "red" 
           | "black"
           | "white"

%import common.NUMBER  
%import common.HEXDIGIT
%import common.SIGNED_NUMBER
%import common.SIGNED_NUMBER
%import common.WS
%ignore WS